buildscript {
	ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
	ext.readPropertyWithDefault = { paramName, defaultValue ->
		if (project.hasProperty(paramName)) {
			return project.getProperties().get(paramName)
		} else {
			Properties properties = new Properties()
			if (project.rootProject.file('local.properties').exists()) {
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
			}
			if (properties.getProperty(paramName) != null) {
				return properties.getProperty(paramName)
			} else {
				return defaultValue
			}
		}
	}

	ext{
		kotlin_version = "1.9.20"
		ksp_version = "1.9.20-1.0.13"

		ubiqueUrl = System.getenv('UBIQUE_ARTIFACTORY_URL') ?: readPropertyWithDefault('ubiqueArtifactoryUrl', '')
		ubiqueUser = System.getenv('UBIQUE_ARTIFACTORY_USER') ?: readPropertyWithDefault('ubiqueArtifactoryUser', '')
		ubiquePass = System.getenv('UBIQUE_ARTIFACTORY_PASS') ?: readPropertyWithDefault('ubiqueArtifactoryPass', '')
		ubiqueMaven = {
			url = ubiqueUrl
			credentials {
				username = ubiqueUser
				password = ubiquePass
			}
			authentication {
				basic(BasicAuthentication)
				digest(DigestAuthentication)
			}
		}
	}
	repositories {
		google()
		maven ubiqueMaven
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:8.1.2'
		classpath "com.google.devtools.ksp:com.google.devtools.ksp.gradle.plugin:$ksp_version"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

		classpath 'com.vanniktech:gradle-maven-publish-plugin:0.25.3'
		classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.9.10'
	}
}

repositories {
	google()
	maven ubiqueMaven
	mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'kotlin-parcelize'

import com.vanniktech.maven.publish.SonatypeHost

android {
	namespace 'io.openmobilemaps.mapscore'

	compileSdk 34

	defaultConfig {
		minSdk 26
		targetSdk 34
		versionName VERSION_NAME
		versionCode Integer.parseInt(VERSION_CODE)

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles 'consumer-rules.pro'
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=c++_shared"
				cppFlags "-std=c++17 -frtti -fexceptions -O2"
			}
		}
	}

	externalNativeBuild {
		cmake {
			path "CMakeLists.txt"
		}
	}

	sourceSets {
		main {
			java {
				srcDir 'src/main/java'
				srcDir '../bridging/android/java'
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}

	buildFeatures {
		prefabPublishing true
	}

	prefab {
		mapscore {
			headers ".cpp_includes"
			name "mapscore"
		}
	}

	buildTypes {
		release {
			ndk {
				debugSymbolLevel 'SYMBOL_TABLE'
			}
		}
	}

	packagingOptions {
		exclude('**/libc++_shared.so')
	}
}

project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	compilerOptions.jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11
}

task copyHeaders() {
	String headersDir = ".cpp_includes"
	project.delete(headersDir)
	project.mkdir(headersDir)
	ConfigurableFileCollection collection = project.files(
			"src/main/cpp",
			"../bridging/android/jni",
			"../shared/public",
			"../shared/src")
	project.copy {
		duplicatesStrategy(DuplicatesStrategy.FAIL)
		from collection.asFileTree.getFiles()
		include("**/*.h")
		into project.file(headersDir)
	}
}
preBuild.dependsOn copyHeaders


afterEvaluate {
	tasks.named("bundleReleaseLocalLintAar").configure {
		dependsOn("prefabReleaseConfigurePackage")
	}
}

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

publishing {
	repositories {
		maven {
			name = "UbiqueMaven"
			url = ubiqueUrl
			credentials {
				username = ubiqueUser
				password = ubiquePass
			}
		}
	}
}

mavenPublishing {
	publishToMavenCentral(SonatypeHost.DEFAULT, true)
	signAllPublications()
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	implementation 'androidx.annotation:annotation:1.7.0'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

	implementation 'com.squareup.moshi:moshi:1.15.0'
	ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.15.0'
	implementation 'com.squareup.okhttp3:okhttp:4.11.0'

	implementation 'ch.ubique.android:djinni-support-lib:1.0.4'

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

clean.doLast {
	project.delete("${projectDir}/build")
	project.delete("${projectDir}/.cxx")
	project.delete("${projectDir}/.gradle")
}