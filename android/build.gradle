buildscript {
	ext.kotlin_version = "1.8.0"
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:7.4.0"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

		classpath 'com.vanniktech:gradle-maven-publish-plugin:0.24.0'
		classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.7.10'
	}
}

repositories {
	google()
	mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'kotlin-kapt'

import com.vanniktech.maven.publish.SonatypeHost

android {
	compileSdkVersion 33

	defaultConfig {
		minSdkVersion 26
		targetSdkVersion 33
		versionName VERSION_NAME
		versionCode Integer.parseInt(VERSION_CODE)

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles 'consumer-rules.pro'
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=c++_shared"
				cppFlags "-std=c++17 -frtti -fexceptions -O2"
			}
		}
	}

	externalNativeBuild {
		cmake {
			path "CMakeLists.txt"
		}
	}

	sourceSets {
		main {
			java {
				srcDir 'src/main/java'
				srcDir '../bridging/android/java'
				srcDir '../external/djinni/support-lib/java'
			}
		}
	}

	buildFeatures {
		prefabPublishing true
	}

	prefab {
		mapscore {
			headers ".cpp_includes"
			name "mapscore"
		}
	}

	buildTypes {
		release {
			ndk {
				debugSymbolLevel 'SYMBOL_TABLE'
			}
		}
	}
}

task copyHeaders() {
	String headersDir = ".cpp_includes"
	project.delete(headersDir)
	project.mkdir(headersDir)
	ConfigurableFileCollection collection = project.files(
			"src/main/cpp",
			"../bridging/android/jni",
			"../shared/public",
			"../shared/src")
	project.copy {
		duplicatesStrategy(DuplicatesStrategy.FAIL)
		from collection.asFileTree.getFiles()
		include("**/*.h")
		into project.file(headersDir)
	}
}
preBuild.dependsOn copyHeaders


afterEvaluate {
	tasks.named("bundleReleaseLocalLintAar").configure {
		dependsOn("prefabReleaseConfigurePackage")
	}
}

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

publishing {
	repositories {
		maven {
			name = "UbiqueMaven"
			url = System.getenv('UBIQUE_ARTIFACTORY_URL') ?: ''
			credentials {
				username = System.getenv('UBIQUE_ARTIFACTORY_USER') ?: ''
				password = System.getenv('UBIQUE_ARTIFACTORY_PASS') ?: ''
			}
		}
	}
}

mavenPublishing {
	publishToMavenCentral(SonatypeHost.DEFAULT, true)
	signAllPublications()
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	implementation "androidx.annotation:annotation:1.5.0"
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

	implementation("com.squareup.moshi:moshi:1.14.0")
	kapt "com.squareup.moshi:moshi-kotlin-codegen:1.14.0"
	implementation 'com.squareup.okhttp3:okhttp:4.10.0'

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

clean.doLast {
	project.delete("${projectDir}/build")
	project.delete("${projectDir}/.cxx")
}